dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/js/js_demo.cxx)
AM_INIT_AUTOMAKE(plib_examples, 1.5.2)


dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL

AC_LANG_CPLUSPLUS


dnl Command line arguments.

dnl Don't use AC_HELP_STRING here as old versions of autoconf do not
dnl understand it.
AC_ARG_WITH(GL,
[  --with-GL=DIR           set the prefix directory where GL resides],
GL_PREFIX=$withval, GL_PREFIX=auto)

if test "x$GL_PREFIX" != "xauto"; then
    LDFLAGS="$LDFLAGS -L$GL_PREFIX/lib"
    CPPFLAGS="$CPPFLAGS -I$GL_PREFIX/include"
fi


dnl Checks for header files.

AC_HEADER_STDC

AC_CHECK_HEADER(windows.h, AC_DEFINE(WIN32))

AC_CANONICAL_HOST

case "${host}" in
*-*-cygwin* | *-*-mingw32*)
    dnl CygWin under Windoze.

    AC_CHECK_HEADERS(GL/gl.h GL/glu.h,,
    AC_MSG_ERROR([OpenGL header files not found]))

    AC_CHECK_HEADER(GL/glut.h, AC_DEFINE(GLUT_IS_PRESENT),
    AC_MSG_ERROR([GLUT header file not found]))
	;;
*-apple-darwin*)
    dnl Mac OS X

    AC_CHECK_HEADERS(OpenGL/gl.h OpenGL/glu.h,,
    AC_MSG_ERROR([OpenGL header files not found]))

    AC_CHECK_HEADER(GLUT/glut.h, AC_DEFINE(GLUT_IS_PRESENT),
    AC_MSG_ERROR([GLUT header file not found]))
	;;
*)
    AC_CHECK_HEADERS(GL/gl.h GL/glu.h,,
    AC_MSG_ERROR([OpenGL header files not found]))

    AC_CHECK_HEADER(GL/glut.h, AC_DEFINE(GLUT_IS_PRESENT))
    AC_CHECK_HEADER(GL/freeglut.h, AC_DEFINE(FREEGLUT_IS_PRESENT))

    if test "x$ac_cv_header_GL_glut_h" = "xno" && test "x$ac_cv_header_GL_freeglut_h" = "xno"; then
        AC_MSG_ERROR([found neither GLUT nor freeglut header file])
    fi
	;;
esac

AC_CHECK_HEADER(plib/ul.h,, AC_MSG_ERROR([PLIB header files not found]))


dnl Checks for library functions.
dnl check for OpenGL related libraries

case "${host}" in
*-*-cygwin* | *-*-mingw32*)
    dnl CygWin under Windoze.

    dnl Multimedia libraries
    SND_LIBS="-lwinmm"
    JS_LIBS="-lwinmm"

    dnl OpenGL libraries
    OGL_LIBS="-lglu32 -lopengl32"
    GLUT_LIBS="-lglut32"
	;;
*-apple-darwin*)
    dnl Mac OS X

    dnl OpenGL libraries
    OGL_LIBS="-framework OpenGL"
    GLUT_LIBS="-framework GLUT -lobjc"

    dnl Sound libraries
    SND_LIBS="-framework Carbon"
	;;
*)
    dnl Check for X11

    AC_PATH_XTRA

    LDFLAGS="$LDFLAGS $X_LIBS"

    OGL_LIBS="$X_PRE_LIBS -lXi -lXmu -lXext -lX11 $X_EXTRA_LIBS -lm"

dnl =========================================================
dnl    if test "x$x_includes" != "x"; then
dnl       CPPFLAGS="$CPPFLAGS -I$x_includes"
dnl    fi
dnl =========================================================

    dnl Reasonable stuff non-windoze variants ... :-)

    AC_CHECK_LIB(dl, dlclose)

    AC_CHECK_LIB(GL, glNewList, OGL_LIBS="-lGL $OGL_LIBS",,$OGL_LIBS)
    if test "x$ac_cv_lib_GL_glNewList" = "xno" ; then
        dnl if no GL, check for MesaGL
        AC_CHECK_LIB(MesaGL, glNewList, OGL_LIBS="-lMesaGL $OGL_LIBS",,
        $OGL_LIBS)

        if test "x$ac_cv_lib_MesaGL_glNewList" = "xno" ; then
                AC_MSG_ERROR([could not find working GL library])
        fi
    fi

    AC_CHECK_LIB(GLU, gluLookAt, OGL_LIBS="-lGLU $OGL_LIBS",,$OGL_LIBS)
    if test "x$ac_cv_lib_GLU_gluLookAt" = "xno" ; then
        dnl if no GLU, check for MesaGLU
        AC_CHECK_LIB(MesaGLU, gluLookAt, OGL_LIBS="-lMesaGLU $OGL_LIBS",,
        $OGL_LIBS)

        if test "x$ac_cv_lib_MesaGLU_gluLookAt" = "xno" ; then
                AC_MSG_ERROR([could not find working GLU library])
	fi
    fi

    dnl check for glut...or freeglut
    if test "x$ac_cv_header_GL_glut_h" = "xyes" ; then
        AC_CHECK_LIB(glut, glutGetModifiers, GLUT_LIBS="-lglut",
        AC_MSG_ERROR([found GLUT header file but no working GLUT library ?!]),
        $OGL_LIBS)
    else
        AC_CHECK_LIB(freeglut, glutGetModifiers, GLUT_LIBS="-lfreeglut",
        AC_MSG_ERROR([found freeglut header file but no working freeglut library ?!]),
        $OGL_LIBS)
    fi

    dnl Check SGI audio library
    AC_CHECK_LIB(audio, ALopenport)

    if test "x$ac_cv_lib_audio_ALopenport" = "xyes" ; then
    dnl this is an SGI machine...
        SND_LIBS="-laudio"
    fi

    CPPFLAGS="$CPPFLAGS -I/usr/local/include"
    LDFLAGS="$LDFLAGS -L/usr/local/lib"
	;;
esac

AC_SUBST(OGL_LIBS)
AC_SUBST(GLUT_LIBS)
AC_SUBST(SND_LIBS)
AC_SUBST(JS_LIBS)


dnl Check PLIB library
AC_CHECK_LIB(plibul, ulInit, true,
AC_MSG_ERROR([there seems to be a problem with the PLIB libraries]))


dnl Warning flags
WFLAG="-Wall"
case "$host" in
mips-sgi-irix*)
    if test "x$CXX" = "xCC"; then
        WFLAG="-fullwarn";
    fi
	;;
esac

CFLAGS="$CFLAGS $WFLAG"
CXXFLAGS="$CXXFLAGS $WFLAG"


AC_OUTPUT( \
	Makefile \
	src/Makefile \
	src/js/Makefile \
	src/pui/Makefile \
	src/sg/Makefile \
	src/sl/Makefile \
	src/ssg/Makefile \
	src/ssg/tux/Makefile \
	src/ssg/majik/Makefile \
	src/ssg/load_save/Makefile \
	src/ssg/state_test/Makefile \
	src/ssg/tween_test/Makefile \
	src/ssg/viewer/Makefile \
	src/fnt/Makefile \
	src/net/Makefile \
	src/net/client_server/Makefile \
	src/net/http_get/Makefile \
	src/net/net_echo/Makefile \
	src/util/Makefile )

