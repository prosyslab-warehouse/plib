dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/ttt3d.cxx)
AM_INIT_AUTOMAKE(ttt3d, 0.0.0)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

plib_suffix="-lplibsl -lplibssgaux -lplibssg -lplibpu -lplibfnt -lplibsg -lplibul"
 
AC_LANG_CPLUSPLUS

dnl Checks for library functions.
dnl check for OpenGL related libraries

AC_CANONICAL_HOST

case "${host}" in
*-*-cygwin* | *-*-mingw32*)
  dnl CygWin under Windoze.

  dnl OpenGL libraries
  LIBS="$plib_suffix $LIBS -lwinmm -lglut32 -lglu32 -lopengl32 -luser32 -lgdi32"
	;;
*)
    dnl Check for X11

    AC_PATH_XTRA

    x_suffix="$X_LIBS $X_PRE_LIBS -lX11 -lXi -lXext -lXmu $X_EXTRA_LIBS -lm"

dnl =========================================================
dnl    if test "x$x_includes" != "x"; then
dnl       CPPFLAGS="$CPPFLAGS -I$x_includes"
dnl    fi
dnl =========================================================

    dnl Reasonable stuff non-windoze variants ... :-)

    AC_CHECK_LIB(dl, dlclose)

    AC_CHECK_LIB(GL, glNewList,,,$x_suffix)
    if test "x$ac_cv_lib_GL_glNewList" = "xno" ; then
        dnl if no GL, check for MesaGL
        AC_CHECK_LIB(MesaGL, glNewList,,,$x_suffix)
    fi

dnl dnl if using mesa, check for xmesa.h
dnl if test "x$ac_cv_lib_MesaGL_glNewList" = "xyes" ; then
dnl     AC_CHECK_HEADER(GL/xmesa.h)
dnl     AM_CONDITIONAL(ENABLE_XMESA_FX, \
dnl                    test "x$ac_cv_header_GL_xmesa_h" = "xyes")
dnl else
dnl     dnl force a failed check
dnl     AM_CONDITIONAL(ENABLE_XMESA_FX, test "no" = "yes")
dnl fi

    AC_CHECK_LIB(GLU, gluLookAt,,,$x_suffix)
    if test "x$ac_cv_lib_GLU_gluLookAt" = "xno" ; then
        dnl if no GLU, check for MesaGLU
        AC_CHECK_LIB(MesaGLU, gluLookAt,,,$x_suffix)
    fi

    opengl_LIBS="$LIBS $x_suffix"

    dnl check for glut...or freeglut
    AC_CHECK_LIB(freeglut, glutGetModifiers,,,$opengl_LIBS)
    if test "x$ac_cv_lib_freeglut_glutGetModifiers" = "xno" ; then
        dnl if no freeglut, check for Real GLUT
        AC_CHECK_LIB(glut, glutGetModifiers,,,$opengl_LIBS)
    fi

    CFLAGS="$CFLAGS -I/usr/local/include -L/usr/local/lib"
    CXXFLAGS="$CXXFLAGS -I/usr/local/include -L/usr/local/lib"

	LIBS="$plib_suffix $LIBS $x_suffix"
	;;
esac

dnl Check SGI audio library
AC_CHECK_LIB(audio, ALopenport)

if test "x$ac_cv_lib_audio_ALopenport" = "xyes" ; then
  dnl this is an SGI machine...
  LIBS="$LIBS -laudio"
fi

dnl Checks for header files.

AC_HEADER_STDC

AC_CHECK_HEADERS(GL/gl.h GL/glut.h)
AC_CHECK_HEADER(windows.h, AC_DEFINE(WIN32))
AC_CHECK_HEADER(joystick.h, AC_DEFINE(JOYSTICK_IS_PRESENT))
AC_CHECK_HEADER(linux/joystick.h, AC_DEFINE(LINUX_JOYSTICK_IS_PRESENT))

dnl Check for audio support

AM_CONDITIONAL(ENABLE_AUDIO, \
        test -r /usr/include/soundcard.h \
                -o -r /usr/include/linux/soundcard.h \
                -o -r /usr/include/machine/soundcard.h \
                -o -r /usr/include/audio.h \
                -o "x$ac_cv_header_windows_h" = "xyes" )

AM_CONDITIONAL(ENABLE_IRIX_AUDIO, test -r /usr/include/audio.h)

AM_CONDITIONAL(ENABLE_WIN32_AUDIO, test "x$ac_cv_header_windows_h" = "xyes")

CFLAGS="$CFLAGS -Wall"
CXXFLAGS="$CXXFLAGS -Wall"

MY_EXPAND_DIR(ttt3d, "$datadir/games/$PACKAGE")
AC_DEFINE_UNQUOTED(TTT3D_DATADIR, "$ttt3d_datadir")

AC_OUTPUT( Makefile         \
           data/Makefile    \
           doc/Makefile     \
           fonts/Makefile   \
           images/Makefile  \
           mods/Makefile    \
           src/Makefile     \
           wavs/Makefile    \
          )

